prototype(Neos.Fusion:Case).@class = 'Neos\\Fusion\\FusionObjects\\CaseImplementation'
prototype(Neos.Fusion:Matcher).@class = 'Neos\\Fusion\\FusionObjects\\MatcherImplementation'
prototype(Neos.Fusion:TestRenderer).@class = 'Neos\\Fusion\\Tests\\Functional\\View\\Fixtures\\TestRenderer'

case.numericMatching = Neos.Fusion:Case {
  100.condition = ${true}
  100.type = 'Neos.Fusion:Case.Test1'

  20.condition = ${cond}
  20.type = 'Neos.Fusion:Case.Test2'
}

case.positionalMatching = Neos.Fusion:Case {
  foo.condition = ${true}
  foo.type = 'Neos.Fusion:Case.Test1'
  foo.@position = 'end'

  z.condition = ${cond}
  z.type = 'Neos.Fusion:Case.Test2'
}



prototype(Neos.Fusion:Case.Test1) < prototype(Neos.Fusion:TestRenderer) {
  test = 'testconditionfalse'
}

prototype(Neos.Fusion:Case.Test2) < prototype(Neos.Fusion:TestRenderer) {
  test = 'testconditiontrue'
}

case.renderPath = Neos.Fusion:Case {
  foo.condition = ${cond}
  foo.renderPath = '/renderPathRenderer/true'

  default.condition = ${true}
  default.renderPath = '/renderPathRenderer/false'
  default.@position = 'end'
}

case.renderPathWillWin < case.renderPath
case.renderPathWillWin {
  foo.type = 'Neos.Fusion:Case.Test1'
}

renderPathRenderer.false = Neos.Fusion:Case.Test1
renderPathRenderer.true = Neos.Fusion:Case.Test2

case.ignoredPropertiesAreIgnored < case.numericMatching {
  @ignoreProperties = ${[1]}
  1 = Neos.Fusion:TestRenderer {
    test = 'somethingwhichshallnotbeshown'
  }
}

case.renderer = Neos.Fusion:Case {
  foo.condition = ${cond}
  foo.renderer = Neos.Fusion:TestRenderer {
    test = 'testconditiontrue'
  }

  default.condition = ${true}
  default.renderer = Neos.Fusion:TestRenderer {
    test = 'testconditionfalse'
  }
  default.@position = 'end'
}

case.rendererWithType < case.renderer {
  foo.type = 'Neos.Fusion:Case.Test1'
  default.type = 'Neos.Fusion:Case.Test1'
}

case.rendererWithRenderPath < case.renderer {
  foo.renderPath = '/renderPathRenderer/false'

  default.renderPath = '/renderPathRenderer/false'
}

case.rendererWorksWithEelAndSimpleTypes = Neos.Fusion:Case {
  foo.condition = ${cond}
  foo.renderer = 'Xtestconditiontrue'

  default.condition = ${true}
  default.renderer = ${'Xtestconditionfalse'}
  default.@position = 'end'
}

case.rendererHasAccessToThis = Neos.Fusion:Case {
  test1 {
    layout = "foo"
    condition = ${this.layout ? true : false}
    renderer = ${this.layout}
  }
}
