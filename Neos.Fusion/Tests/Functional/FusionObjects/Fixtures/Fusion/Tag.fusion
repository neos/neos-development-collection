prototype(Neos.Fusion:DataStructure).@class = 'Neos\\Fusion\\FusionObjects\\DataStructureImplementation'
prototype(Neos.Fusion:Attributes).@class = 'Neos\\Fusion\\FusionObjects\\AttributesImplementation'
prototype(Neos.Fusion:DataStructure).@class = 'Neos\\Fusion\\FusionObjects\\DataStructureImplementation'
prototype(Neos.Fusion:Tag) {
  @class = 'Neos\\Fusion\\FusionObjects\\TagImplementation'
  attributes = Neos.Fusion:DataStructure
  omitClosingTag = false
  selfClosingTag = false
}

tag.plainAttributes = Neos.Fusion:Tag {
  tagName = 'link'
  selfClosingTag = true
  attributes {
    rel = 'stylesheet'
    type = 'text/css'
  }
}

tag.objectAttributes = Neos.Fusion:Tag {
  tagName = 'test'
  selfClosingTag = true
  attributes {
    sum = ${2 + 2}
  }
}

tag.arrayAttributes = Neos.Fusion:Tag {
  attributes {
    class = Neos.Fusion:DataStructure
    class.a = 'a'
    class.b = 'b'
  }
}

tag.fusionAttributes = Neos.Fusion:Tag {
  attributes = Neos.Fusion:Attributes {
    key = 'value'
    list = ${['foo', 'bar']}
  }
}

tag.dataStructureAttributes = Neos.Fusion:Tag {
  attributes = Neos.Fusion:DataStructure {
    key = 'value'
    list = ${['foo', 'bar']}
  }
}

tag.booleanAttributes = Neos.Fusion:Tag {
  attributes = Neos.Fusion:DataStructure {
    foo = true
    bar = false
    baz = null
  }
}

tag.booleanAttributesAndForbiddenEmptyAttributes = Neos.Fusion:Tag {
  allowEmptyAttributes = false
  attributes = Neos.Fusion:DataStructure {
    foo = true
    bar = false
    baz = null
  }
}

tag.booleanAttributesAndAllowEmptyAttributes = Neos.Fusion:Tag {
  allowEmptyAttributes = true
  attributes = Neos.Fusion:DataStructure {
    foo = true
    bar = false
    baz = null
  }
}

tag.plainContent = Neos.Fusion:Tag {
  tagName = 'span'
  content = 'test'
}

tag.objectContent = Neos.Fusion:Tag {
  tagName = 'span'
  content = ${2 + 2}
}

tag.registeredSelfClosingTag = Neos.Fusion:Tag {
  tagName = 'br'
}

tag.omitClosingTag = Neos.Fusion:Tag {
  tagName = 'test'
  omitClosingTag = true
}

tag.withThis = Neos.Fusion:Tag {
  tagName = 'title'
  attributes {
    databar = 'baz'
    datafoo = ${this.databar + '_baz'}
  }
  content = 'foo'
}

tag.withIgnorePropertiesInAttributes = Neos.Fusion:Tag {
  tagName = 'title'
  attributes {
    @ignoreProperties = ${['databar']}
    databar = 'baz'
    datafoo = ${this.databar + '_baz'}
  }
  content = 'foo'
}
