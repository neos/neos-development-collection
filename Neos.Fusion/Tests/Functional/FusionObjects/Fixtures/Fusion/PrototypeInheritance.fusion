///////// SIMPLE PROTOTYPE INHERITANCE

// First, set up a base object...
prototype(Neos.Fusion:PrototypeInheritance.Base) {
  @class = 'Neos\\Fusion\\FusionObjects\\ValueImplementation'

  value = 'BaseNotModified'
}

// Then subclass it. changes to the subclass should only be visible in the subclass.
prototype(Neos.Fusion:PrototypeInheritance.SubWithOverride) < prototype(Neos.Fusion:PrototypeInheritance.Base) {
  value = 'Sub'
}

// subclass it again, without modification...
prototype(Neos.Fusion:PrototypeInheritance.SubWithoutOverride) < prototype(Neos.Fusion:PrototypeInheritance.Base)

// change the parent class; this should be reflected in "SubWithOverride" and "Base"
// -- THIS IS THE ACTUAL PROTOTYPE INHERITANCE TEST!
prototype(Neos.Fusion:PrototypeInheritance.Base).value = 'BaseModified'

prototypeInheritance {
  base = PrototypeInheritance.Base
  subWithOverride = PrototypeInheritance.SubWithOverride
  subWithoutOverride = PrototypeInheritance.SubWithoutOverride
}

///////// ADVANCED PROTOTYPE INHERITANCE
// Here, we test context-dependent prototypes such as prototype(Foo).prototype(Bar).
// This should now also take the inheritance into account, if Foo e.g. has a parent type.

prototype(Neos.Fusion:PrototypeInheritance.Advanced.Value).@class = 'Neos\\Fusion\\FusionObjects\\ValueImplementation'
prototype(Neos.Fusion:PrototypeInheritance.Advanced.WrappedNested.Base).@class = 'Neos\\Fusion\\Tests\\Functional\\FusionObjects\\Fixtures\\WrappedNestedObjectRenderer'
prototype(Neos.Fusion:PrototypeInheritance.Advanced.WrappedNested.Base) {
  prototype(PrototypeInheritance.Advanced.Value).value = '|value_from_base_NEVER_TO_BE_SHOWN|'
  prepend = 'prepend_beforeOverride'
  append = 'append_beforeOverride'
  value = PrototypeInheritance.Advanced.Value
}
prototype(Neos.Fusion:PrototypeInheritance.Advanced.WrappedNested.SubWithOverride) < prototype(Neos.Fusion:PrototypeInheritance.Advanced.WrappedNested.Base) {
  prepend = 'prepend_inSub'
}

prototype(Neos.Fusion:PrototypeInheritance.Advanced.WrappedNested.SubWithoutOverride) < prototype(Neos.Fusion:PrototypeInheritance.Advanced.WrappedNested.Base)

prototype(Neos.Fusion:PrototypeInheritance.Advanced.WrappedNested.Base) {
  append = 'append_afterOverride'
  prototype(PrototypeInheritance.Advanced.Value).value = '|value_from_nested_prototype|'
}

prototypeInheritanceAdvanced {
  // when evaluating "base", the following should happen:
  // - prepend_beforeOverride
  // - value_from_nested_prototype
  // - append_afterOverride
  base = PrototypeInheritance.Advanced.WrappedNested.Base

  // same as "base"; as nothing has changed. THIS IS WHERE PROTOTYPE INHERITANCE SHOWS
  subWithoutOverride = PrototypeInheritance.Advanced.WrappedNested.SubWithoutOverride

  // when evaluating this, the following should happen:
  // - prepend_inSub
  // - value_from_nested_prototype
  // - append_afterOverride
  subWithOverride = PrototypeInheritance.Advanced.WrappedNested.SubWithOverride
}



///////// CONTEXT DEPENDENT PROTOTYPES WITH INHERITANCE
// Test setup:
// - have the "Value" prototype and a "SubclassedValue" prototype inheriting from the first one
// - do a context specific override for the *Value* prototype; setting its value to a different element.
// - render the "SubclassedValue"; expect that the *overridden* value from the base class applies
prototype(Neos.Fusion:PrototypeInheritance.ContextDependent.Value) {
  value = "should never be shown"
  @class = 'Neos\\Fusion\\FusionObjects\\ValueImplementation'
}

prototype(Neos.Fusion:PrototypeInheritance.ContextDependent.SubclassedValue) < prototype(Neos.Fusion:PrototypeInheritance.ContextDependent.Value)

prototypeInheritanceContentDependent {
  prototype(Neos.Fusion:PrototypeInheritance.ContextDependent.Value).value = "NEW VALUE in base class"

  element = Neos.Fusion:PrototypeInheritance.ContextDependent.SubclassedValue
}
