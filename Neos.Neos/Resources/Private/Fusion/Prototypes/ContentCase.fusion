# Neos.Neos:ContentCase inherits Neos.Fusion:Case and overrides the default case
# with a catch-all condition for the default case, setting the type variable to
# the name of the current nodes' node type
#
prototype(Neos.Neos:ContentCase) < prototype(Neos.Fusion:Case) {

  default {
    @position = 'end 9999'
    condition = Neos.Fusion:CanRender {
      type = ${node.nodeType.name}
    }
    type = ${node.nodeType.name}
  }

  rawContent {
    @position = 'end 10000'
    condition = ${documentNode.context.inBackend && documentNode.context.currentRenderingMode.edit}
    renderer = afx`
      <script src={StaticResource.uri('Neos.Neos', 'Public/JavaScript/RawContent.js')}></script>
      <neos-raw-content
        styles={StaticResource.uri('Neos.Neos', 'Public/Styles/Minimal.css') + ',' + StaticResource.uri('Neos.Neos', 'Public/Styles/RawContentMode.css')}
        style="all: unset;"
      >
        <Neos.Neos:RawContent.Node />
      </neos-raw-content>
    `
  }

  # Fail but create a helpful error message
  error {
    @position = 'end 10001'
    condition = true
    type = ${documentNode.nodeType.name}
  }
}
