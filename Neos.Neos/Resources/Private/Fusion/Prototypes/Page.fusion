# Neos.Neos:Page is the default object used for rendering in most sites
#
prototype(Neos.Neos:Page) >
prototype(Neos.Neos:Page) < prototype(Neos.Fusion:Http.Message) {

  httpResponseHead {
    headers {
      'Content-Type' = 'text/html'
    }
  }

  doctype = '<!DOCTYPE html>'
  doctype.@position = 'start 100'

  # Only the opening html tag for the page. This is done to avoid deep nesting of Fusion paths for integrators.
  htmlTag = Neos.Fusion:Tag {
    @position = 'start'
    tagName = 'html'
    omitClosingTag = true
  }

  headerComment = ${Configuration.setting('Neos.Neos.headerComment')}
  headerComment.@position = 'before headTag'

  # The opening head tag for the page. This is done to avoid deep nesting of Fusion paths for integrators.
  headTag = Neos.Fusion:Tag {
    @position = 'after htmlTag'
    tagName = 'head'
    omitClosingTag = true
  }

  # The content of the head tag, integrators can add their own head content.
  head = Neos.Fusion:Join {
    @position = 'after headTag'

    characterSetMetaTag = Neos.Fusion:Tag {
      @position = 'start 10'
      tagName = 'meta'
      attributes {
        charset = 'UTF-8'
      }
    }

    titleTag = Neos.Fusion:Tag {
      tagName = 'title'
      content = ${q(node).property('title')}
    }

    # Link tags for stylesheets in the head should go here
    stylesheets = Neos.Fusion:Join

    # Script includes in the head should go here
    javascripts = Neos.Fusion:Join {
      @position = 'after stylesheets'
    }
  }

  closingHeadTag = '</head>'
  closingHeadTag.@position = 'after head'

  # The opening body tag for the page. This is done to avoid deep nesting of Fusion paths for integrators.
  bodyTag = Neos.Fusion:Tag {
    @position = '20'
    tagName = 'body'
    omitClosingTag = true
    attributes.class.@process.addNeosBackendClass = ${Array.push(value, 'neos-backend')}
    attributes.class.@process.addNeosBackendClass.@if.onlyRenderWhenNotInLiveWorkspace = ${renderingMode.isEdit}
  }

  # Content of the body tag. To be defined by the integrator.
  body = Neos.Fusion:Join {
    @position = 'after bodyTag'

    # Scripts before the closing body tag
    javascripts = Neos.Fusion:Join
    javascripts.@position = 'end 100'
  }

  closingBodyTag = '</body>'
  closingBodyTag.@position = 'end 100'

  closingHtmlTag = '</html>'
  closingHtmlTag.@position = 'end 200'


  @cache {
    mode = 'cached'
    entryIdentifier {
      documentNode = ${Neos.Caching.entryIdentifierForNode(node)}
    }
    entryTags {
      1 = ${Neos.Caching.nodeTag(node)}
    }
  }

  @process.debugDump = Neos.Fusion:DebugDump

  @exceptionHandler = 'Neos\\Neos\\Fusion\\ExceptionHandlers\\PageHandler'
}
