/**
 * Only for internal use!
 * @internal
 */
prototype(Neos.Neos:Core.BackendPage) < prototype(Neos.Fusion:Component) {
  title = ''
  content = ''
  additionalResources = Neos.Fusion:Join

  @private {
    headerComment = ${Configuration.setting('Neos.Neos.headerComment')}
  }

  renderer = Neos.Fusion:Join {
    doctype = '<!DOCTYPE html>'
    html = afx`
      <html lang="en">
      {private.headerComment}
      <head>
        <meta charset="utf-8"/>
        <meta name="robots" content="noindex,nofollow"/>
        <title>{props.title}</title>
        <link href={StaticResource.uri('Neos.Neos', 'Public/Images/Icons/apple-touch-icon.png')} sizes="180x180" rel="apple-touch-icon"/>
        <link href={StaticResource.uri('Neos.Neos', 'Public/Images/Icons/favicon-16x16.png')} sizes="16x16" rel="icon" type="image/png"/>
        <link href={StaticResource.uri('Neos.Neos', 'Public/Images/Icons/favicon-32x32.png')} sizes="32x32" rel="icon" type="image/png"/>
        <link href={StaticResource.uri('Neos.Neos', 'Public/Images/Icons/safari-pinned-tab.svg')} rel="mask-icon" color="#00adee"/>
        {props.additionalResources}
      </head>
      {props.content}
      </html>
    `
  }
}
