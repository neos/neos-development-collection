Neos.Workspace.Ui.WorkspaceController.index = Neos.Fusion:Component {
  /// string
  userWorkspaceName = ${userWorkspaceName}
  /// Neos\Workspace\Ui\ViewModel\WorkspaceListItems
  workspaceListItems = ${workspaceListItems}
  /// array
  flashMessages = ${flashMessages}

  newAction = Neos.Fusion:UriBuilder {
    action = 'new'
    format = 'htmx'
  }

  createNewPopoverId = 'create-workspace-popover'

  i18n = ${I18n.id('').source('Main').package('Neos.Workspace.Ui')}

  renderer = Neos.Fusion:Match {
    @subject = ${request.format}
    @default = afx`
      <section
        hx-indicator="#indicator"
        hx-boost="true"
      >
        <Neos.Workspace.Ui:Component.HTMXConfig/>
        <Neos.Workspace.Ui:Component.FlashMessages flashMessages={props.flashMessages}/>
        <Neos.Workspace.Ui:Component.Indicator/>
        <main class="neos-content neos-container-fluid">
          <div class="neos-row-fluid">
            <Neos.Workspace.Ui:Component.WorkspaceTable
              userWorkspaceName={props.userWorkspaceName}
              workspaceListItems={props.workspaceListItems}
            />
          </div>
        </main>
        <Neos.Workspace.Ui:Component.Footer>
          <button
            id="createButton"
            type="button"
            class="neos-button neos-button-success"
            hx-get={props.newAction}
            hx-target="#popover-container"
            hx-swap="innerHTML"
            hx-on--after-request={'document.getElementById("' + props.createNewPopoverId + '").showPopover()'}
            @if={Security.hasAccess('Neos.Workspace.Ui:Backend.CreateWorkspaces')}
          >
            {props.i18n.id('workspaces.createNewWorkspace')}
          </button>
          <Neos.Workspace.Ui:Component.WorkspaceCount
            workspaceCount={Array.length(props.workspaceListItems)}
          />
        </Neos.Workspace.Ui:Component.Footer>
      </section>
      <section id="popover-container"></section>
    `
    htmx = afx`
      <Neos.Workspace.Ui:Component.FlashMessages flashMessages={props.flashMessages}/>
      <Neos.Workspace.Ui:Component.WorkspaceTable
        userWorkspaceName={props.userWorkspaceName}
        workspaceListItems={props.workspaceListItems}
      />
    `
  }
}
