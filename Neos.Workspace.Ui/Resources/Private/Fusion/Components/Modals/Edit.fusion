prototype(Neos.Workspace.Ui:Component.Modal.Edit) < prototype(Neos.Fusion:Component) {
  workspaceHasChanges = false
  /// array<string, string>
  baseWorkspaceOptions = ${[]}

  workspaceName = ''
  workspaceTitle = ''
  workspaceDescription = ''
  baseWorkspaceName = ''

  @private {
    i18n = ${I18n.id('').source('Main').package('Neos.Workspace.Ui')}
    popoverId = 'workspace-edit-modal'
    workspaceTableRowId = ${'workspace-row-' + props.workspaceName}
  }

  prototype(Neos.Fusion.Form:LabelRenderer) {
    translationPackage = 'Neos.Workspace.Ui'
    translationSource = 'Main'
  }

  prototype(Neos.Fusion.Form:Neos.BackendModule.FieldContainer) {
    translation.label {
      package = 'Neos.Workspace.Ui'
      source = 'Main'
    }
  }

  renderer = afx`
    <div popover id={private.popoverId}>
      <header>
        <button
          type="button"
          class="neos-close neos-button"
          popovertarget={private.popoverId}
          popovertargetaction="close"
        >
          <Neos.Workspace.Ui:Component.Icon icon="times"/>
        </button>
        <div class="neos-header">
          {private.i18n.id('workspaces.editWorkspace').arguments([props.workspaceTitle])}
        </div>
      </header>
      <section>
        <Neos.Fusion.Form:Form
          form.target.action="update"
          form.target.format="htmx"
          form.target.arguments.workspaceName={props.workspaceName}
          attributes.class="neos-inline"
          attributes.hx-post={form.getTarget()}
          attributes.hx-disabled-elt="find button"
          attributes.hx-on--after-request={'document.getElementById("' + private.popoverId + '").hidePopover()'}
        >
          <fieldset>
            <Neos.Fusion.Form:Neos.BackendModule.FieldContainer
              field.name="title"
              field.value={props.workspaceTitle}
              label="workspaces.workspace.title"
              class="neos-control-group"
            >
              <Neos.Fusion.Form:Textfield
                attributes.required
                attributes.pattern="/^[\p{L}\p{P}\d \.]{1,200}$/u"
                attributes.autofocus
                attributes.placeholder={props.workspaceName}
              />
            </Neos.Fusion.Form:Neos.BackendModule.FieldContainer>

            <Neos.Fusion.Form:Neos.BackendModule.FieldContainer
              field.name="description"
              field.value={props.workspaceDescription}
              label="workspaces.workspace.description"
              class="neos-control-group"
            >
              <Neos.Fusion.Form:Textfield
                attributes.pattern="/^[\p{L}\p{P}\d \.]{0,500}$/u"
              />
            </Neos.Fusion.Form:Neos.BackendModule.FieldContainer>

            <Neos.Fusion.Form:Neos.BackendModule.FieldContainer
              field.name="baseWorkspace"
              field.value={props.baseWorkspaceName}
              label="workspaces.workspace.baseWorkspace"
              class="neos-control-group"
            >
              <Neos.Fusion.Form:Select
                attributes.disabled={props.workspaceHasChanges}
              >
                <Neos.Fusion:Loop items={props.baseWorkspaceOptions} itemName="workspaceTitle" itemKey="workspaceName">
                  <Neos.Fusion.Form:Select.Option
                    option.value={workspaceName}
                    attributes.selected={workspaceName == props.baseWorkspaceName}
                  >
                    {workspaceTitle}
                  </Neos.Fusion.Form:Select.Option>
                </Neos.Fusion:Loop>
              </Neos.Fusion.Form:Select>
              <p
                style={{ marginTop: '.5em' }}
                @if.workspaceHasChanges={props.workspaceHasChanges}
              >
                <i
                  class="fas fa-exclamation-triangle"
                  style={{ color: 'var(--warningText)', marginRight: '.5em' }}
                ></i>{' '}
                {private.i18n.id('workspaces.cantChangeBaseWorkspaceBecauseWorkspaceContainsChanges')}
              </p>
            </Neos.Fusion.Form:Neos.BackendModule.FieldContainer>
          </fieldset>
          <fieldset>
            <!-- TODO internal was removed -->
            <Neos.Fusion:Fragment
              @if={false && Security.hasAccess('Neos.Workspace.Ui:Backend.Module.Management.Workspace.ManageInternalWorkspaces')}>
              <h3 class="neos-control-label">
                {private.i18n.id('workspaces.workspace.visibility')}
              </h3>
              <Neos.Fusion.Form:Neos.BackendModule.FieldContainer
                class="neos-control-group"
              >
                <Neos.Fusion.Form:Radio
                  id="visibility.private"
                  field.value="private"
                  attributes.disabled
                  attributes.checked={props.isPrivateWorkspace}
                >
                  <span></span>
                  <span>
                  {private.i18n.id('workspaces.workspace.visibility.private')}
                    <span class="neos-help-inline">
                    {private.i18n.id('workspaces.workspace.visibility.private.help')}
                  </span>
                </span>
                </Neos.Fusion.Form:Radio>
                <br/>
                <!-- TODO internal was removed -->
                <Neos.Fusion.Form:Radio
                  id="visibility.internal"
                  field.value="internal"
                  attributes.disabled
                  attributes.checked={props.isInternalWorkspace}
                >
                  <span></span>
                  <span>
                  {private.i18n.id('workspaces.workspace.visibility.internal')}
                    <span class="neos-help-inline">
                    {private.i18n.id('workspaces.workspace.visibility.internal.help')}
                  </span>
                </span>
                </Neos.Fusion.Form:Radio>
              </Neos.Fusion.Form:Neos.BackendModule.FieldContainer>
            </Neos.Fusion:Fragment>

            <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary">
              {private.i18n.id('applyChanges')}
            </Neos.Fusion.Form:Button>
          </fieldset>
        </Neos.Fusion.Form:Form>
      </section>
      <footer>
        <button
          type="button"
          class="neos-button"
          popovertarget={private.popoverId}
          popovertargetaction="close"
        >
          {private.i18n.id('cancel', 'Cancel', [])}
        </button>
      </footer>
    </div>
  `
}
