##
# Renders a document for review
#
prototype(Neos.Workspace.Ui:Component.ReviewChangeTableRow) < prototype(Neos.Fusion:Component) {

  relativePath = ''
  change = ${change}
  document = ${document}
  canPublishToBaseWorkspace = ${canPublishToBaseWorkspace}
  selectedWorkspaceName = ${selectedWorkspaceName}

  @private {
    i18n = ${I18n.id('').source('Main').package('Neos.Workspace.Ui')}
  }

  renderer = afx`
    <tr class={"neos-change fold-" + document.documentNode.aggregateId.value + "document-" + document.documentNode.aggregateId.value} data-nodepath={relativePath} data-ismoved={change.isMoved} data-isnew={change.isNew}>

      <td id={"change-" + change.serializedNodeAddress} colspan="4">
        <Neos.Fusion:DataStructure
          @path="attributes.class"
          general="neos-content-change"
          deleted={change.isRemoved ? 'legend-deleted' : ''}
          created={change.isNew ? 'legend-created' : ''}
          moved={change.isMoved ? 'legend-moved' : ''}
          hidden={change.hidden ? 'legend-hidden': ''}
        />
        <Neos.Workspace.Ui:Component.ReviewChangeDiff
          change={change}
          relativePath={relativePath}
          document={document}/>
      </td>
    </tr>
  `
}
